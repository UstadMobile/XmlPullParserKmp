plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.4.10'
    id 'kotlin-android-extensions'
    id 'com.android.library'
    id 'maven-publish'
}

group = 'com.ustadmobile'
version = '0.0.1'
def localProperties = new Properties()
localProperties.load(new FileInputStream(project.file("local.properties")))

repositories {
    google()
    mavenCentral()
}

kotlin {
    jvm {
        compilations.all {
            kotlinOptions.jvmTarget = '1.8'
        }
        testRuns["test"].executionTask.configure {
            useJUnit()
        }
    }
    js(LEGACY) {
        browser {
            testTask {
                useKarma {
                    useChrome()
                    webpackConfig.cssSupport.enabled = true
                }
            }
        }
    }
    android()
    sourceSets {
        commonMain {

        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation "net.sf.kxml:kxml2:2.3.0"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test-junit')
            }
        }
        jsMain {

        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
        androidMain {
            dependencies {
                implementation "com.google.android.material:material:1.2.0"
            }
        }
        androidTest {
            dependencies {
                implementation kotlin('test-junit')
                implementation 'junit:junit:4.13'
            }
        }
    }
}

android {
    compileSdkVersion 30
    sourceSets.main.manifest.srcFile('src/androidMain/AndroidManifest.xml')
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
    }
}

publishing {
    repositories {
        maven {
            name = "XmlPullParserKmp"
            url = uri("https://maven.pkg.github.com/UstadMobile/XmlPullParserKmp")
            credentials {
                username = localProperties["gpr.user"]
                password = localProperties["gpr.key"]
            }
        }
    }
}
